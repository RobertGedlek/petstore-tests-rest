plugins {
    id 'java'
    id "org.openapi.generator" version "7.2.0"
    id 'io.qameta.allure' version '2.12.0'
}

group = 'info.gedlek'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

allure {
    version = '2.24.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'org.assertj:assertj-core:3.25.1'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'io.qameta.allure:allure-junit5:2.25.0'
    testImplementation 'io.qameta.allure:allure-rest-assured:2.25.0'

    testImplementation 'ch.qos.logback:logback-classic:1.2.13'

    testImplementation 'net.datafaker:datafaker:2.1.0'
}

def swaggerFile = layout.buildDirectory.file("swagger-downloaded.json").get().asFile

tasks.register('downloadSwagger') {
    doLast {
        swaggerFile.parentFile.mkdirs()
        new URL("https://petstore.swagger.io/v2/swagger.json").withInputStream { i ->
            swaggerFile.withOutputStream { it << i }
        }
        println "âœ… Downloaded : ${swaggerFile.path}"
    }
}

openApiGenerate {
    generatorName = "java"
    inputSpec = swaggerFile.path
    outputDir = layout.buildDirectory.dir("generated").get().asFile.path
    apiPackage = "info.gedlek.api"
    modelPackage = "info.gedlek.model"

    configOptions = [
            library: "resttemplate",
            dateLibrary: "java8",
            serializationLibrary: "jackson",
            useJakartaEe: "false"
    ]

    globalProperties = [
            models: "",
            modelDocs: "false",
            apis: "false",
            apiDocs: "false",
            modelTests: "false"
    ]
}
tasks.register('compile') {
    group = 'build'
    description = 'Generates sources and compiles the project.'
    dependsOn 'testClasses'
}

tasks.named('openApiGenerate') {
    dependsOn 'downloadSwagger'
}

tasks.named('compileJava') {
    dependsOn 'openApiGenerate'
}

sourceSets {
    main {
        java {
            srcDir layout.buildDirectory.dir("generated/src/main/java")
        }
    }
}

test {
    useJUnitPlatform()

    doFirst {
        //remove old allure results
        file("build/allure-results").deleteDir()
        println "ðŸ§¹ Allure results directory cleaned!"
    }
}

configurations.all {
    resolutionStrategy {
        force 'org.aspectj:aspectjweaver:1.9.22'
    }
}